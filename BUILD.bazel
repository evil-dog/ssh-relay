load("@bazel_gazelle//:def.bzl", "gazelle")

# gazelle:prefix github.com/hazaelsan/ssh-relay
# gazelle:proto package
# gazelle:proto_group go_package
gazelle(name = "gazelle")

filegroup(
    name = "coverage_files",
    srcs = glob(["bazel-out/**/coverage.dat"]),
)

genrule(
    name = "coverage_gcov",
    outs = ["coverage.gcov"],
    cmd = " ".join([
        "$(locations @com_github_wadey_gocovmerge//:gocovmerge)",
        "--",
        "$(locations //:coverage_files)",
        ">$@",
    ]),
    tags = ["manual"],
    tools = [
        ":coverage_files",
        "@com_github_wadey_gocovmerge//:gocovmerge",
    ],
)

genrule(
    name = "coverage_lcov",
    outs = ["coverage.lcov"],
    cmd = " ".join([
        "GOPATH=$(GOPATH)",
        "$(locations @com_github_jandelgado_gcov2lcov//:gcov2lcov)",
        "--infile=$(locations :coverage_gcov)",
        "--outfile=$@",
    ]),
    tags = ["manual"],
    tools = [
        ":coverage_gcov",
        "@com_github_jandelgado_gcov2lcov//:gcov2lcov",
    ],
)

genrule(
    name = "coverage_html",
    outs = ["coverage"],
    cmd = "genhtml --output-directory $@ $(locations :coverage_lcov)",
    tags = [
        "local",
        "manual",
    ],
    tools = [":coverage_lcov"],
)
